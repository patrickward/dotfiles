#
# zshenv -> zprofile -> zshrc -> zlogin
#
# Short to this dotfiles path is $DOTDIR
export DOTDIR=$HOME/.dotfiles

# Projects folder
export PROJECTS=$HOME/Code

# Stash local environment variables in ~/.localrc so that
# they stay out of the dotfiles repository and not available to the public
if [[ -a $HOME/.localrc ]]
then
  source $HOME/.localrc
fi

# Find all zsh files in each directory under zsh
typeset -U config_files
config_files=($DOTDIR/zsh/**/*.zsh)

# Source all path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# Load everything except the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
#
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# Set the prompt
# Using Pure from https://github.com/sindresorhus/pure
# Files: zsh/pure.sh, zsh/async.sh
autoload -U promptinit; promptinit
prompt pure
