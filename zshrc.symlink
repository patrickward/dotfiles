# PROFILE: zmodload zsh/zprof
# TEST: for i in $(seq 1 10); do /usr/bin/time zsh -i -c exit; done
#
# zshenv -> zprofile -> zshrc -> zlogin
#

# NOTE: SEE zshenv.symlink for all of the sourced files

# Initialize autocomplete here, otherwise functions won't be loaded
# autoload -U compinit
# compinit
autoload -Uz compinit
_comp_files=(${ZDOTDIR:-$HOME}/.zcompdump(Nm-20))
if (( $#_comp_files )); then
  compinit -i -C
else
  compinit -i
fi
unset _comp_files

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
#
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# Run the rbenv command later in the process
source ~/.dotfiles/zsh/ruby/rbenv.sh

# Need to add anaconda after rbenv as rbenv
# reset the path and interferes with anaconda
if [[ "$OSTYPE" == darwin* ]]; then
  export PATH="$HOME/anaconda3/bin:$PATH"
  export PATH="/usr/local/opt/mysql@5.7/bin:$PATH"
fi

# Set the prompt
# Using Pure from https://github.com/sindresorhus/pure
# Files: zsh/pure.sh, zsh/async.sh
autoload -U promptinit; promptinit

# Add precmd, so that a conda env can be seen prior to the `pure` prompt
precmd() {
    if [ ! -z "$CONDA_DEFAULT_ENV" ]; then
        local condaenv
        condaenv=`basename $CONDA_DEFAULT_ENV`
        # Grey=242
        PROMPT="%F{242}($condaenv)%f %(?.%F{magenta}.%F{red})‚ùØ%f "
    fi
}

prompt pure

# END_PROFILE: zprof

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

# Finally, ensure tmux is running
ensure_tmux_is_running
